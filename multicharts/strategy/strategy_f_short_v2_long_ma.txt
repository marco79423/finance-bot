// TXF 43min
inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: OpenPrice(Open);
if 0845 <= Time and Time <= 0850 then begin
	OpenPrice = Open;
end;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

if MarketPosition = 0 then begin
	vars: ShortCond(True);
	ShortCond = True;
	ShortCond = ShortCond and not (OverMonth and 1300 < Time and Time < 1400);
	ShortCond = ShortCond and not (0845 <= Time and Time <= 0847);
	ShortCond = ShortCond and Close < Open;
	ShortCond = ShortCond and Close < AverageFC(Close, 70);
	ShortCond = ShortCond and Close = Lowest(Close, 3);
	ShortCond = ShortCond and Ticks = Highest(Ticks, 15);
	if ShortCond then
		SellShort ( "SE" ) OrderSize contracts next bar market;

end else if MarketPosition = -1 then begin
	if OverMonth and 1300 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	vars: BuyToCoverCond(True);
	BuyToCoverCond = True;
	BuyToCoverCond = BuyToCoverCond and Close > AverageFC(Close, 32);
	if BuyToCoverCond then
		BuyToCover ("SX") all contracts next bar market;


	if OpenPositionProfit > 250 * BigPointValue * OrderSize then
		BuyToCover ("SX_MaxWin") all contracts next bar market;

	if BarsSinceEntry > 9 then begin
		BuyToCover ("SX_Giveup") all contracts next bar market;
	end;

	if OpenPositionProfit < -185 * BigPointValue * OrderSize then begin
		BuyToCover ("SX_StopLoss") all contracts next bar market;
	end;
end;
