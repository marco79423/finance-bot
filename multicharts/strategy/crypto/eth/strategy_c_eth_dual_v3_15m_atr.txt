// ETH 15min
// Use bar 45

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: RevisedOrderSize(OrderSize);
if Round(60000 / Close, 0) > 1 then begin
	RevisedOrderSize = Round(60000 / Close, 0) * OrderSize;
end else begin
	RevisedOrderSize = OrderSize;
end;

var:ATR(0);
ATR = AvgTrueRange(45);

var: InMultiplier(2.8);
if MarketPosition <= 0 then begin
	if Close > Close[1] + ATR[1] * InMultiplier then begin
		Buy ("LE") RevisedOrderSize contracts next bar market;
	end;
end;

if MarketPosition >= 0 then begin
	if Close < Close[1] - ATR[1] * InMultiplier then begin
		SellShort ("SE") RevisedOrderSize contracts next bar market;
	end;
end;

vars: TimeoutBars(155), OutMultiplier(10);
if MarketPosition = 1 then begin
	if BarsSinceEntry > TimeoutBars and OpenPositionProfit < 0 then begin
		Sell ("LX_Time") all contracts next bar market;
	end;

	if Close < EntryPrice - ATR[1] * OutMultiplier then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
	if BarsSinceEntry > TimeoutBars and OpenPositionProfit < 0 then begin
		BuyToCover ("SX_Time") all contracts next bar market;
	end;

	if High > EntryPrice + ATR[1] * OutMultiplier then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;
