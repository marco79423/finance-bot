// BTC 30 min
// Backtest time: 2019/10/01 - 2024/01/01 +00:00

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: RevisedOrderSize(OrderSize);
if Round(100000 / Close, 0) > 1 then begin
	RevisedOrderSize = Round(100000 / Close, 0) * OrderSize;
end else begin
	RevisedOrderSize = OrderSize;
end;


vars: KBar(35), BBandMultiplier(3.3);
vars: BBandUp(0), BBandDown(0);
BBandUp = BollingerBand(Close, KBar, BBandMultiplier);
BBandDown = BollingerBand(Close, KBar, -BBandMultiplier);

if Close > BBandUp then begin
	Buy ("LE") RevisedOrderSize contracts next bar market;
end;

if Close < BBandDown then begin
	SellShort ( "SE" ) RevisedOrderSize contracts next bar market;
end;

vars: PullBackPercent(0.04), StopLossPercent(0.01);

if MarketPosition = 1 then begin
	if HighSinceEntry > EntryPrice then begin
		Sell ("LX_PT") all contracts next bar HighSinceEntry - EntryPrice * PullBackPercent stop;
	end;
	Sell ("LX_ST") all contracts next bar EntryPrice * (1 - StopLossPercent) stop;
end;

if MarketPosition = -1 then begin
	if LowSinceEntry < EntryPrice then begin
		BuyToCover ("SX_PT") all contracts next bar LowSinceEntry + EntryPrice * PullBackPercent stop;
	end;
	BuyToCover ("SX_ST") all contracts next bar EntryPrice * (1 + StopLossPercent) stop;
end;
