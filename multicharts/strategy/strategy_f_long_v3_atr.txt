//TXF 48 min
inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;


var: KBar(40), BBandUp(0), ATR(0);
BBandUp = BollingerBand(Close, KBar, 2) ;
ATR = AvgTrueRange(KBar) ;

//Over day reset
var:HH(99999);
if D > D[1] then begin
	HH = OpenD(0) + ATR / 2 ;
end;

var: EntryHigh(0);
if MarketPosition = 1 then begin
	if High > EntryHigh then
		EntryHigh = High;
end else begin
	EntryHigh = 0;
end;

//Entry
if MarketPosition = 0 then begin
	vars: BuyCond(True);
	BuyCond = not (OverMonth and 1200 < Time and Time < 1400);
	BuyCond = BuyCond and not (0845 <= Time and Time <= 0847);
	BuyCond = BuyCond and High > BBandUp;
	BuyCond = BuyCond and Close > Open;
	BuyCond = BuyCond and Close > HH; // test
	if BuyCond then
		Buy ("LE") OrderSize contracts next bar market;

end else if MarketPosition > 0 then begin
	if OverMonth and 1200 < Time and Time < 1430 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if Close < EntryHigh - ATR * 2 then
		Sell ("LX") all contracts next bar market;

	if Close > BollingerBand(Close, KBar, 4) and Close < Low then
	 	Sell ("LX_TooHigh") all contracts next bar market;
end;
