// MNQ 43 min
// Use bar 135
// For swing trading
// Focus on win rate and smooth out the curve

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: ATR(0);
ATR = AvgTrueRange(14);

vars: OpenCond(True);
OpenCond = True;
OpenCond = not IsSettlementDay("MNQ");

if MarketPosition = 0 then begin
	vars: BuyCond(False);
	BuyCond = OpenCond;
	BuyCond = BuyCond and Close > Open;
	BuyCond = BuyCond and Close > AverageFC(Close, 5);
	BuyCond = BuyCond and Close > AverageFC(Close, 135);
	if BuyCond then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;

end else begin
	if IsSettlementDay("MNQ") then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 1.5 * ATR * BigPointValue * OrderSize then begin
		Sell("LX_Win") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - 2 * ATR * BigPointValue * OrderSize then begin
		Sell ("LX_Pullback") all contracts next bar market;
	end;

	if OpenPositionProfit < -3.5 * ATR * BigPointValue * OrderSize then begin
		Sell("LX_StopLoss") all contracts next bar market;
	end;

end;
