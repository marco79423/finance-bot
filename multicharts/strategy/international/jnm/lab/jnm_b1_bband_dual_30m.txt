// JNM 20 min
// Backtest time: 2018/01/01 - 2024/01/01 Exchange
// Compliance: 1


inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;


vars: MALength(3), KBar(135), BBandMultiplier(2), ADXLength(14), ADXMax(29), BasePercent(0.07), LossMultiplier(1);

vars: MA(0);
MA = Average(Close, MALength);

vars: BBandDown(0), BBandUp(0);
BBandDown = BollingerBand(Close, KBar, -BBandMultiplier);
BBandUp = BollingerBand(Close, KBar, BBandMultiplier);


if ADX(ADXLength) < ADXMax then begin
	if Close > MA and Close < BBandDown then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;

	if Close < MA and Close > BBandUp then begin
		SellShort ("SE") OrderSize contracts next bar market;
	end;
end;

vars: ProfitPercent(0), StopLossPercent(0);
ProfitPercent = BasePercent;
StopLossPercent = BasePercent * LossMultiplier;

if MarketPosition = 1 then begin
	if Close > (1 + ProfitPercent) * EntryPrice then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Close < (1 - StopLossPercent) * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
	if Close < (1 - ProfitPercent) * EntryPrice then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if Close > (1 + StopLossPercent) * EntryPrice then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;
