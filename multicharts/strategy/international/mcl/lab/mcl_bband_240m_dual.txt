// MCL 240 min
// Backtest time: 2022/01/20 00:00 - 2024/06/01 Exchange
// Use bar 34

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;


vars: KBar(45), BBandMultiplier(1.5);
vars: BBandUp(0), BBandDown(0);
BBandUp = BollingerBand(Close, KBar, BBandMultiplier);
BBandDown = BollingerBand(Close, KBar, -BBandMultiplier);

vars: OpenCond(True);

vars: BuyCond(True);
BuyCond = OpenCond;
BuyCond = BuyCond and Close cross over BBandDown;
if BuyCond then begin
	Buy ("LE") OrderSize contracts next bar market;
end;

vars: ShortCond(True);
ShortCond = OpenCond;
ShortCond = ShortCond and Close cross under BBandUp;
if ShortCond then begin
	SellShort ("SE") OrderSize contracts next bar market;
end;


vars: PullBack_Percent(0.028), StopLoss_Percent(0.040);

if MarketPosition = 1 then begin

	if OpenPositionProfit > 0 and Close < HighSinceEntry - EntryPrice * PullBack_Percent then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Low < EntryPrice - StopLoss_Percent * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;


if MarketPosition < 0 then begin

	if OpenPositionProfit > 0 and Close > LowSinceEntry + EntryPrice * PullBack_Percent then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if High > EntryPrice + StopLoss_Percent * EntryPrice then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;
