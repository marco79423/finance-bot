// TXF 15 min
// Backtest time: 2014/01/01 - 2014/01/01 Exchange
// Compliance: 1

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;


vars: StdDevLength(105), StdDevMultiplier(1.9), PullBackPercent(0.085), StopLossPercent(0.025);

vars: BigK(0);
BigK = StandardDev(Close, StdDevLength, 1) * StdDevMultiplier;

vars: OpenCond(True);
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

if OpenCond and MarketPosition <= 0 then begin
	Buy ("LE") OrderSize contracts next bar AvgPrice + BigK stop;
end;

if OpenCond and MarketPosition >= 0 then begin
	SellShort ("SE") OrderSize contracts next bar AvgPrice - BigK stop;
end;

if MarketPosition = 1 then begin
	if HighSinceEntry > EntryPrice then begin
		Sell ("LX_PT") all contracts next bar HighSinceEntry - EntryPrice * PullBackPercent stop;
	end;
	Sell ("LX_ST") all contracts next bar EntryPrice * (1 - StopLossPercent) stop;
end;

if MarketPosition = -1 then begin
	if LowSinceEntry < EntryPrice then begin
		BuyToCover ("SX_PT") all contracts next bar LowSinceEntry + EntryPrice * PullBackPercent stop;
	end;
	BuyToCover ("SX_ST") all contracts next bar EntryPrice * (1 + StopLossPercent) stop;
end;

