// TXF 15 min
// Backtest time: 2018/01/01 - 2014/01/01 Exchange
// Fee (12 + 5) * 20
// Compliance: 1


inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: KBar(33), BBandMultiplier(3.6), PullBackPercent(0.026), StopLossPercent(0.045);

vars: BBandUp(0), BBandDown(0);
BBandUp = BollingerBand(Close, KBar, BBandMultiplier);
BBandDown = BollingerBand(Close, KBar, -BBandMultiplier);

if Close > BBandUp then begin
	Buy ("LE") OrderSize contracts next bar market;
end;

if Close < BBandDown then begin
	SellShort ( "SE" ) OrderSize contracts next bar market;
end;

if MarketPosition = 1 then begin
	if OpenPositionProfit > 0 and Close < HighSinceEntry - EntryPrice * PullBackPercent then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Low < EntryPrice - StopLossPercent * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
	if OpenPositionProfit > 0 and Close > LowSinceEntry + EntryPrice * PullBackPercent then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if High > EntryPrice + StopLossPercent * EntryPrice then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;
