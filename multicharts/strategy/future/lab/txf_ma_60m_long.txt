// TXF 60 min
// Use bar 260

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: OpenCond(True);
OpenCond = True;
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

if MarketPosition <= 0 then begin
	vars: BuyCond(False);
	BuyCond = OpenCond;
	BuyCond = BuyCond and Close > AverageFC(Close, 260);
	BuyCond = BuyCond and Close > AverageFC(Close, 120);
	BuyCond = BuyCond and Ticks = Highest(Ticks, 3);
	if BuyCond then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;
end else begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	vars: PullBack_Percent(0.004), StopLoss_Percent(0.006);

	if OpenPositionProfit > 0 and Close < HighSinceEntry - EntryPrice * PullBack_Percent then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Low < EntryPrice - StopLoss_Percent * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;
