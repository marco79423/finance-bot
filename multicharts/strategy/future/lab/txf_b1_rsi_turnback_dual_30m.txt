// TXF 30 min
// Backtest time: 2018/01/01 - 2024/01/01 Exchange
// Compliance: 1

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: RSILen(8), RSIOverSold(20), RSIOverBought(95), ProfitPercent(0.025), StopLossPercent(0.035);

vars: RSIValue(0);
RSIValue = RSI(Close, RSILen);

vars: OpenCond(True);
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

if MarketPosition = 0 and OpenCond then begin
	if RSIValue < RSIOverSold then begin
		Buy ( "LE" ) OrderSize contracts next bar market;
	end;

	if RSIValue > RSIOverBought then begin
		SellShort ( "SE" ) OrderSize contracts next bar market;
	end;
end;

if MarketPosition = 1 then begin
    if Close > (1 + ProfitPercent) * EntryPrice then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Close < (1 - StopLossPercent) * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
    if Close < (1 - ProfitPercent) * EntryPrice then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if Close > (1 + StopLossPercent) * EntryPrice then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;

