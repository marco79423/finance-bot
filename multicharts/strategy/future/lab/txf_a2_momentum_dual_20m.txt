// TXF 20 min
// Backtest time: 2018/01/01 - 2014/01/01 Exchange
// Compliance: 1

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;


vars: LookBackN(200), BasePercent(0.008), ProfitMultiplier(1.2), MaxEntriesPerDay(2);

vars: ProfitPercent(0), StopLossPercent(0);
ProfitPercent = ProfitMultiplier * BasePercent;
StopLossPercent = BasePercent;

vars: OpenCond(True);
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);
OpenCond = OpenCond and EntriesToday(Date) < MaxEntriesPerDay;

if OpenCond and MarketPosition <= 0 then begin
	Buy ("LE") OrderSize contracts next bar Highest(High, LookBackN) stop;
end;

if OpenCond and MarketPosition >= 0 then begin
	SellShort ("SE") OrderSize contracts next bar Lowest(Low, LookBackN) stop;
end;

if MarketPosition = 1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

    if Close > (1 + ProfitPercent) * EntryPrice then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Close < (1 - StopLossPercent) * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

    if Close < (1 - ProfitPercent) * EntryPrice then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if Close > (1 + StopLossPercent) * EntryPrice then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;
