// TXF 10 min
// Backtest time: 2018/01/01 - 2014/01/01 Exchange
// Compliance: 1

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;


vars: EffLen(65), EffLongThreshold(0.35), EffShortThreshold(0.36), BasePercent(0.01), ProfitMultiplier(1.3);

vars: ProfitPercent(0), StopLossPercent(0);
ProfitPercent = ProfitMultiplier * BasePercent;
StopLossPercent = BasePercent;

vars: EffPointSum(0), EffWindow(0), EffRatio(0);

EffWindow = EffLen - 1;
EffPointSum = Summation(AbsValue(Close - Close[1]), EffWindow) ;

if EffPointSum <> 0 then begin
	EffRatio = (Close - Close[EffWindow]) / EffPointSum;
end else begin
	EffRatio = 0;
end;

vars: OpenCond(True);
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

if MarketPosition <= 0 then begin
	if OpenCond and crosses_above(EffRatio, EffLongThreshold) then begin
		Buy ( "LE" ) OrderSize contracts next bar market;
	end;
end;

if MarketPosition >= 0 then begin
	if OpenCond and crosses_below(EffRatio, -EffShortThreshold) then begin
		SellShort ( "SE" ) OrderSize contracts next bar market;
	end;
end;

if MarketPosition = 1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

    if Close > (1 + ProfitPercent) * EntryPrice then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Close < (1 - StopLossPercent) * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

    if Close < (1 - ProfitPercent) * EntryPrice then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if Close > (1 + StopLossPercent) * EntryPrice then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;

