// TXF 480 min
// Backtest time: 2014/01/01 - 2014/01/01 Exchange
// Use bar 5


inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: KBar(4), BBandMultiplier(1);
vars: BBandUp(0), BBandDown(0);
BBandUp = BollingerBand(Close, KBar, BBandMultiplier);
BBandDown = BollingerBand(Close, KBar, -BBandMultiplier);

vars: OpenCond(True);
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

vars: BuyCond(True);
BuyCond = OpenCond;
BuyCond = BuyCond and Close cross over BBandDown;
if BuyCond then begin
	Buy ("LE") OrderSize contracts next bar market;
end;

vars: ShortCond(True);
ShortCond = OpenCond;
ShortCond = ShortCond and Close cross under BBandUp;
if ShortCond then begin
	SellShort ("SE") OrderSize contracts next bar market;
end;


vars: PullBack_Percent(0.004), StopLoss_Percent(0.03);

if MarketPosition = 1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and Close < HighSinceEntry - EntryPrice * PullBack_Percent then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Low < EntryPrice - StopLoss_Percent * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;


if MarketPosition < 0 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and Close > LowSinceEntry + EntryPrice * PullBack_Percent then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if High > EntryPrice + StopLoss_Percent * EntryPrice then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;
