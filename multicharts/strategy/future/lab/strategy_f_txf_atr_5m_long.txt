// TXF 5 min
// Use bar 7

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: OpenCond(True);
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

vars: Len(7), ATR(0);
ATR = AvgTrueRange(Len);

vars: LongMultiplier(7);
if MarketPosition <= 0 then begin
	if OpenCond and Close > Close[1] + ATR[1] * LongMultiplier then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;
end;

if MarketPosition = 1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	vars: PullBack_Percent(0.008), StopLoss_Percent(0.004);

	if OpenPositionProfit > 0 and Close < HighSinceEntry - EntryPrice * PullBack_Percent then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Low < EntryPrice - StopLoss_Percent * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;
