// TXF 15 min
// Use bar 25
// https://hiskio.com/courses/396/lectures/20618

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: OpenCond(True);
OpenCond = True;
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

vars: Len(25), NN(3), BigK(0);
BigK = StandardDev(Close, Len, 1) * NN ;
if BigK < 10 then begin
	BigK = 10;
end;

if MarketPosition <= 0 then begin
	vars: BuyCond(True);
	BuyCond = OpenCond;
	BuyCond = BuyCond and Close > AvgPrice[1] + BigK;
	if BuyCond then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;
end;

if MarketPosition >= 0 then begin
	vars: ShortCond(True);
	ShortCond = OpenCond;
	ShortCond = ShortCond and Close < AvgPrice[1] - BigK;
	if ShortCond then begin
		SellShort ("SE") OrderSize contracts next bar market;
	end;
end;

if MarketPosition = 1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if Close < HighSinceEntry - 2.5 * BigK then begin
		Sell ("LX_Out") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	if Close > LowSinceEntry + 0.5 * BigK then begin
		BuyToCover ("SX_Out") all contracts next bar market;
	end;
end;

vars: ATR(0);
ATR = AvgTrueRange(3);
SetStopLoss(4.5 * ATR * BigPointValue);
