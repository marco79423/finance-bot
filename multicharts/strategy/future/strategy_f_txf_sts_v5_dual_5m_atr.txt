// TXF 5min
// Use bar 10

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: OpenCond(True);
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

vars: ATR(0);
ATR = AvgTrueRange(10);

vars: LongMultiplier(2.5), ShortMultiplier(13);
if MarketPosition <= 0 then begin
	if OpenCond and Close > Close[1] + ATR[1] * LongMultiplier then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;
end;

if MarketPosition >= 0 then begin
	if OpenCond and Close < Close[1] - ATR[1] * ShortMultiplier then begin
		SellShort ("SE") OrderSize contracts next bar market;
	end;
end;

vars: SellMultiplier(8), CoverMultiplier(4.5) ;
if MarketPosition = 1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if Close < EntryPrice - ATR[1] * SellMultiplier then begin
		Sell ("LX_Out") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	if High > EntryPrice + ATR[1] * CoverMultiplier then begin
		BuyToCover ("SX_Out") all contracts next bar market;
	end;
end;

SetStopLoss(9 * ATR * BigPointValue);
