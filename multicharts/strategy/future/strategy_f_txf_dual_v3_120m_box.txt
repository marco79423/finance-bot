// TXF 120 min
// Use bar 35
// For swing trading
// Focus on balancing profits and losses.

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: OpenCond(True);
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

vars: Len(35), BoxH(0), BoxL(0);
BoxH = Highest(High, Len) ;
BoxL = Lowest(Low, Len) ;

if MarketPosition <= 0 then begin
	if High = BoxH then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;
end;

if MarketPosition >= 0 then begin
	if Low = BoxL then begin
		SellShort ( "SE" ) OrderSize contracts next bar market;
	end;
end;

vars: HC(0), LC(0);
if MarketPosition > 0 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	var: Base(0.015);
	if Close > EntryPrice + (Base * EntryPrice * 2) then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Low < EntryPrice - (Base * EntryPrice * 1) then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;

if MarketPosition < 0 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	var: Base2(0.01);
	if Close < EntryPrice - (Base2 * EntryPrice * 2) then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if High > EntryPrice + (Base2 * EntryPrice * 1) then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;
