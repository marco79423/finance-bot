// TXF 47 min
// Use bar 540
// For swing trading
// Focus on balancing profits and losses.

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: OpenCond(True);
OpenCond = True;
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

var: KBar(540), BBandDown(0), ATR(0);
BBandDown = BollingerBand(Close, KBar, -1);
ATR = AvgTrueRange(KBar);

var:LL(0);
if D > D[1] then begin
	LL = OpenD(0) - ATR;
end;

var: EntryLow(99999);
if MarketPosition = -1 then begin
	if Low < EntryLow then
		EntryLow = Low;
end else begin
	EntryLow = 99999;
end;

if MarketPosition = 0 then begin
	vars: ShortCond (True);
	ShortCond = OpenCond;
	ShortCond = ShortCond and Close < AverageFC(Close, 4);
	ShortCond = ShortCond and Close < AverageFC(Close, 32);
	ShortCond = ShortCond and RSI(Close, 100) > 40;
	ShortCond = ShortCond and Low < BBandDown;
	ShortCond = ShortCond and Close < LL;
	ShortCond = ShortCond and Ticks = Highest(Ticks, 2);
	if ShortCond then begin
		SellShort ("SE") OrderSize contracts next bar market;
	end;

end else begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	vars: Base(0), BP(1);
	Base = AvgTrueRange(60);
	BP =  OpenPositionProfit / (Base * BigPointValue * OrderSize);
	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - (3.95 - BP) * Base * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback") all contracts next bar market;
	end;

	if Close > EntryLow + ATR * 3.2 then begin
		BuyToCover ("SX_GiveUp") all contracts next bar market;
	end;
end;
