// TXF 15 min
// Backtest time: 2018/01/01 - 2014/01/01 Exchange
// Fee (12 + 5) * 20
// Compliance: 1

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;


vars: KBar(95), BBandMultiplier(1.5), PullBackPercent(0.011), StopLossPercent(0.016);
vars: BBandUp(0), BBandDown(0);
BBandUp = BollingerBand(Close, KBar, BBandMultiplier);
BBandDown = BollingerBand(Close, KBar, -BBandMultiplier);

vars: OpenCond(True);
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

if OpenCond and Close cross over BBandDown then begin
	Buy ("LE") OrderSize contracts next bar market;
end;

if OpenCond and Close cross under BBandUp then begin
	SellShort ("SE") OrderSize contracts next bar market;
end;

if MarketPosition = 1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and Close < HighSinceEntry - EntryPrice * PullBackPercent then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Low < EntryPrice - StopLossPercent * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;


if MarketPosition < 0 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and Close > LowSinceEntry + EntryPrice * PullBackPercent then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if High > EntryPrice + StopLossPercent * EntryPrice then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;
