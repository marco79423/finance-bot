// TXF 4h
// Use bar 70

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: OpenCond(True);
OpenCond = True;
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

if MarketPosition <= 0 then begin
	vars: BuyCond(False);
	BuyCond = OpenCond;
	BuyCond = BuyCond and Close > AverageFC(Close, 2);
	BuyCond = BuyCond and Close > AverageFC(Close, 25);
	if BuyCond then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;
end;

if MarketPosition >= 0 then begin
	vars: ShortCond(True);
	ShortCond = OpenCond;
	ShortCond = ShortCond and Close < AverageFC(Close, 10);
	ShortCond = ShortCond and Close < AverageFC(Close, 80);
	if ShortCond then begin
		SellShort ( "SE" ) OrderSize contracts next bar market;
	end;
end;

vars: PullBack_Percent(0.04), StopLoss_Percent(0.02);
if MarketPosition > 0 then begin
	if Close < HighSinceEntry - EntryPrice * PullBack_Percent then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Low < EntryPrice - StopLoss_Percent * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;
end;

if MarketPosition < 0 then begin
	if Close > LowSinceEntry + EntryPrice * PullBack_Percent then begin
		BuyToCover ("SX_PT") all contracts next bar market;
	end;

	if High > EntryPrice + StopLoss_Percent * EntryPrice then begin
		BuyToCover ("SX_SL") all contracts next bar market;
	end;
end;
