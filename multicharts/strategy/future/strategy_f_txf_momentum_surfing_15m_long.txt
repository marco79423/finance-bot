// TXF 15 min
// Use bar 40
// https://hiskio.com/courses/396/lectures/20618

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: OpenCond(True);
OpenCond = True;
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

vars: Len(40), NN(1.9);
vars: BigK(0);
BigK = StandardDev(Close, Len, 1) * NN ;
if BigK < 10 then begin
	BigK = 9999;
end;

vars: BuyCond(True);
BuyCond = OpenCond;
BuyCond = BuyCond and Close > AvgPrice[1] + BigK;
if BuyCond then begin
	Buy ("LE") OrderSize contracts next bar market;
end;


if MarketPosition = 1 then begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	vars: PullBack_Percent(0.004), StopLoss_Percent(0.007);

	if OpenPositionProfit > 0 and Close < HighSinceEntry - EntryPrice * PullBack_Percent then begin
		Sell ("LX_PT") all contracts next bar market;
	end;

	if Low < EntryPrice - StopLoss_Percent * EntryPrice then begin
		Sell ("LX_SL") all contracts next bar market;
	end;

end;
