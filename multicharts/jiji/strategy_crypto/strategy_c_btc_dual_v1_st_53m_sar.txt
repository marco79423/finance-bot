// BTC 53 min
// Use bar 420
// For swing trading
// Focus on balancing profits and losses.

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: ATR(0);
ATR = AvgTrueRange(14);

vars: AfStep(0.01), AfLimit(0.02), ParCl(0), ParOp(0), Position(0), Transition(0) , SAR(0);
SAR = ParabolicSAR(AfStep, AfLimit, ParCl, ParOp, Position, Transition) ;

vars: OpenCond(True);
OpenCond = True;

vars: BuyCond(True);
BuyCond = OpenCond;
BuyCond = BuyCond and Position = 1;
BuyCond = BuyCond and Close > High[1];
if BuyCond then begin
	Buy ("LE") OrderSize contracts next bar market;
end;

vars: ShortCond(True);
ShortCond = OpenCond;
ShortCond = ShortCond and Position = -1;
ShortCond = ShortCond and Close < AverageFC(Close, 150);
ShortCond = ShortCond and Close < AverageFC(Close, 420);
ShortCond = ShortCond and Close < Low[1];
ShortCond = ShortCond and Ticks = Highest(Ticks, 17);
if ShortCond then begin
	SellShort ("SE") OrderSize contracts next bar market;
end;

if MarketPosition = 1 then begin
	if Position = -1 then begin
		Sell ("LX_Sar") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - 12 * ATR * BigPointValue * OrderSize then begin
		Sell ("LX_Pullback") all contracts next bar market;
	end;

	if OpenPositionProfit < -7 * ATR * BigPointValue * OrderSize then begin
		Sell ("LX_StopLoss") all contracts next bar market;
	end;

end;

if MarketPosition = -1 then begin
	vars: CoverCond(True);
	CoverCond = True;
	CoverCond = CoverCond and Close < AverageFC(Close, 2);
	CoverCond = CoverCond and Ticks = Highest(Ticks, 7);
	if CoverCond then begin
		BuyToCover ("SX_Out") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - 4 * ATR * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback") all contracts next bar market;
	end;

	if BarsSinceEntry > 13 then begin
		BuyToCover ("SX_GiveUp") all contracts next bar market;
	end;

	if OpenPositionProfit < -5 * ATR * BigPointValue * OrderSize then begin
		BuyToCover ("SX_StopLoss") all contracts next bar market;
	end;

end;
