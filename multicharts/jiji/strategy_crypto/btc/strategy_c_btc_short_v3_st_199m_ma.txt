// BTC 199 min
// Use bar 45
// For swing trading
// Focus on balancing profits and losses.

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: ATR(0);
ATR = AvgTrueRange(14);

vars: OpenCond(True);
OpenCond = True;

if MarketPosition = 0 then begin
	vars: ShortCond(True);
	ShortCond = OpenCond;
	ShortCond = ShortCond and Close < Open;
	ShortCond = ShortCond and Close < AverageFC(Close, 45);
	ShortCond = ShortCond and Close = Lowest(Close, 8);
	ShortCond = ShortCond and Ticks = Highest(Ticks, 8);
	if ShortCond then begin
		SellShort ( "SE" ) OrderSize contracts next bar market;
	end;

end else begin

	if OpenPositionProfit > 3 * ATR * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Win") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - 2.5 * ATR * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback") all contracts next bar market;
	end;

	if OpenPositionProfit < -3.5 * ATR * BigPointValue * OrderSize then begin
		BuyToCover ("SX_StopLoss") all contracts next bar market;
	end;
end;
