// TXF 4 min
// For swing trading

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: HH(999999);
if Time = 0925 then begin
	HH = HighD(0);
end;

vars: OpenCond(True);
OpenCond = True;
OpenCond = not (IsSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

If MarketPosition = 0 then begin
	vars: BuyCond(True);
	BuyCond = OpenCond;
	BuyCond = BuyCond and Time >= 0925 and Time < 1300;
	BuyCond = BuyCond and OpenD(0) - CloseD(1) > 15;
	BuyCond = BuyCond and HighD(0) - CloseD(1) > 155;
	BuyCond = BuyCond and EntriesToday(D) <= 0;
	BuyCond = BuyCond and Close > HH;
	if BuyCond then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;
end else begin
	vars: Base(0);
	Base = AvgTrueRange(60);

	vars: BP(1);
	BP =  OpenPositionProfit / (Base * BigPointValue * OrderSize);
	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - (5.1 - BP * 0.5) * Base * BigPointValue * OrderSize then begin
		Sell ("LX_Pullback") all contracts next bar market;
	end;

	if OpenPositionProfit < -160 * BigPointValue * OrderSize then begin
		Sell ("LX_StopLoss") all contracts next bar market;
	end;

	If (IsSettlementDay and Time >= 1300) or (not IsSettlementDay and Time >= 1330) then begin
		sell ("LX_Timeout") next bar market;
	end;
end;
