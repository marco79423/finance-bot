//TXF 53min
inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: AfStep(0.01), AfLimit(0.02), ParCl(0), ParOp(0), Position(0), Transition(0) , SAR(0);
SAR = ParabolicSAR(AfStep, AfLimit, ParCl, ParOp, Position, Transition) ;

vars: EnvCond(True);
EnvCond = True;
EnvCond = not (OverMonth and 1200 < Time and Time < 1400);
EnvCond = EnvCond and not (0845 <= Time and Time <= 0847);

vars: BuyCond(True);
BuyCond = True;
BuyCond = BuyCond and EnvCond;
BuyCond = BuyCond and Position = 1;
BuyCond = BuyCond and Close > AverageFC(Close, 63);
BuyCond = BuyCond and Close > High[1];
if BuyCond then begin
	Buy ("LE") OrderSize contracts next bar market;
end;

vars: ShortCond(True);
ShortCond = True;
ShortCond = ShortCond and EnvCond;
ShortCond = ShortCond and Position = -1;
ShortCond = ShortCond and Close < AverageFC(Close, 186);
ShortCond = ShortCond and Close < Low[1];
if ShortCond then begin
	SellShort ("SE") OrderSize contracts next bar market;
end;

vars: Base(0);
Base = AvgTrueRange(60);

if MarketPosition = 1 then begin
	if OverMonth and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 4.75 * Base * BigPointValue * OrderSize and OpenPositionProfit < MaxOpenPositionProfit - 1.45 * Base * BigPointValue * OrderSize then begin
		Sell ("LX_Pullback") all contracts next bar market;
	end;

	if OpenPositionProfit > 1.65 * Base * BigPointValue * OrderSize and OpenPositionProfit < MaxOpenPositionProfit - 3.70 * Base * BigPointValue * OrderSize then begin
		Sell ("LX_Pullback2") all contracts next bar market;
	end;

	if OpenPositionProfit < -190 * BigPointValue * OrderSize then begin
		Sell ("LX_StopLoss") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
	if OverMonth and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 3.95 * Base * BigPointValue * OrderSize and OpenPositionProfit < MaxOpenPositionProfit - 2.40 * Base * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback") all contracts next bar market;
	end;

	if OpenPositionProfit > 3.05 * Base * BigPointValue * OrderSize and OpenPositionProfit < MaxOpenPositionProfit - 2.65 * Base * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback2") all contracts next bar market;
	end;

	if OpenPositionProfit > 1.50 * Base * BigPointValue * OrderSize and OpenPositionProfit < MaxOpenPositionProfit - 3.45 * Base * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback3") all contracts next bar market;
	end;

	if OpenPositionProfit < -50 * BigPointValue * OrderSize then begin
		BuyToCover ("SX_StopLoss") all contracts next bar market;
	end;
end;
