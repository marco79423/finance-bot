// TXF 53min
// Use 480 bar
// Focus on balancing profits and losses.

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: AfStep(0.01), AfLimit(0.02), ParCl(0), ParOp(0), Position(0), Transition(0) , SAR(0);
SAR = ParabolicSAR(AfStep, AfLimit, ParCl, ParOp, Position, Transition) ;

vars: EnvCond(True);
EnvCond = True;
EnvCond = not (IsSettlementDay and 1200 < Time and Time < 1400);
EnvCond = EnvCond and not (0845 <= Time and Time <= 0847);

vars: BuyCond(True);
BuyCond = True;
BuyCond = BuyCond and EnvCond;
BuyCond = BuyCond and Position = 1;
BuyCond = BuyCond and Close > High[1];
if BuyCond then begin
	Buy ("LE") OrderSize contracts next bar market;
end;

vars: ShortCond(True);
ShortCond = True;
ShortCond = ShortCond and EnvCond;
ShortCond = ShortCond and Position = -1;
ShortCond = ShortCond and Close < AverageFC(Close, 180);
ShortCond = ShortCond and Close < AverageFC(Close, 480);
ShortCond = ShortCond and Close < Low[1];
ShortCond = ShortCond and Ticks = Highest(Ticks, 14);
if ShortCond then begin
	SellShort ("SE") OrderSize contracts next bar market;
end;

if MarketPosition = 1 then begin
	if IsSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if Position = -1 then begin
		Sell ("LX_Sar") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - 190 * BigPointValue * OrderSize then begin
		Sell ("LX_Pullback") all contracts next bar market;
	end;

	if OpenPositionProfit < -400 * BigPointValue * OrderSize then begin
		Sell ("LX_StopLoss") all contracts next bar market;
	end;
end;

if MarketPosition = -1 then begin
	if IsSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	vars: CoverCond(True);
	CoverCond = True;
	CoverCond = CoverCond and Close < AverageFC(Close, 4);
	CoverCond = CoverCond and Ticks = Highest(Ticks, 8);
	if CoverCond then begin
		BuyToCover ("SX_Out") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - 80 * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback") all contracts next bar market;
	end;

	if BarsSinceEntry > 44 then begin
		BuyToCover ("SX_GiveUp") all contracts next bar market;
	end;

 	if OpenPositionProfit < -215 * BigPointValue * OrderSize then begin
		BuyToCover ("SX_StopLoss") all contracts next bar market;
	end;

end;
