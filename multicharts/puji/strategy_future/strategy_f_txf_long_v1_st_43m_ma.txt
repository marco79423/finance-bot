// TXF 43 min
// Use bar 70
// For swing trading
// Focus on win rate and smooth out the curve

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: ATR(0);
ATR = AvgTrueRange(14);

vars: OpenCond(True);
OpenCond = True;
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

if MarketPosition = 0 then begin
	vars: BuyCond(False);
	BuyCond = OpenCond;
	BuyCond = BuyCond and Close > Open;
	BuyCond = BuyCond and Close > AverageFC(Close, 65);
	BuyCond = BuyCond and Ticks = Highest(Ticks, 11);
	if BuyCond then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;

end else begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 4 * ATR * BigPointValue * OrderSize then begin
		Sell("LX_Win") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - 1 * ATR * BigPointValue * OrderSize then begin
		Sell ("LX_Pullback") all contracts next bar market;
	end;

	if BarsSinceEntry > 15 * 25 and OpenPositionProfit > 0 then begin
		Sell ("LX_GiveUp") all contracts next bar market;
	end;

	if OpenPositionProfit < -9 * ATR * BigPointValue * OrderSize then begin
		Sell("LX_StopLoss") all contracts next bar market;
	end;

end;
