// TXF 17 min
// Use bar 25
// For range trading
// Focus on win rate and smooth out the curve.

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: OpenCond(True);
OpenCond = True;
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

var: KBar(25), BBandAverage(0), BBandDown(0);
BBandAverage = Average(Close, KBar);
BBandDown = BBandAverage - 2.9 * StandardDev(Close, KBar, 1);

vars: OutPrice(0);
if MarketPosition = 0 then begin
	vars: BuyCond(True);
	BuyCond = OpenCond;
	BuyCond = BuyCond and Close cross over BBandDown;
	BuyCond = BuyCond and Close > Close[1];
	if BuyCond then begin
		Buy ("LE") OrderSize contracts next bar market;
		OutPrice = BBandAverage;
    end;
end else begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if Close > OutPrice then begin
		Sell ("LX_Out") all contracts next bar market;
	end;
end;
