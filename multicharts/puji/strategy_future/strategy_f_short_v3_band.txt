//TXF 47 min
inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

var: KBar(540), BBandDown(0), ATR(0);
BBandDown = BollingerBand(Close, KBar, -1);
ATR = AvgTrueRange(KBar);

//Over day reset
var:LL(0);
if D > D[1] then begin
	LL = OpenD(0) - ATR;
end;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

var: EntryLow(99999);
if MarketPosition = -1 then begin
	if Low < EntryLow then
		EntryLow = Low;
end else begin
	EntryLow = 99999;
end;

if MarketPosition = 0 then begin
	vars: ShortCond (True);
	ShortCond = not (OverMonth and 1200 < Time and Time < 1400);
	ShortCond = ShortCond and not (0845 <= Time and Time <= 0847);
	ShortCond = ShortCond and Close < AverageFC(Close, 4);
	ShortCond = ShortCond and Close < AverageFC(Close, 32);
	ShortCond = ShortCond and RSI(Close, 100) > 40;
	ShortCond = ShortCond and Low < BBandDown;
	ShortCond = ShortCond and Close < LL;
	ShortCond = ShortCond and Ticks = Highest(Ticks, 2);
	if ShortCond then
		SellShort ("SE") OrderSize contracts next bar market;

end else begin
	if OverMonth and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	vars: Base(0), BP(1);
	Base = AvgTrueRange(60);
	BP =  OpenPositionProfit / (Base * BigPointValue * OrderSize);
	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - (3.95 - BP) * Base * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback") all contracts next bar market;
	end;

	if Close > EntryLow + ATR * 3.2 then
		BuyToCover ("SX_Giveup") all contracts next bar market;
end;
