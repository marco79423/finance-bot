// TXF 47 min
// Use bar 60
// For swing trading
// Focus on balancing profits and losses.

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

var: KBar(37), BBandUp(0), ATR(0);
BBandUp = BollingerBand(Close, KBar, 1.5);
ATR = AvgTrueRange(KBar);

var: HH(99999);
if D > D[1] then begin
	HH = OpenD(0) + ATR * 0.3;
end;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

var: EntryHigh(0);
if MarketPosition = 1 then begin
	if High > EntryHigh then
		EntryHigh = High;
end else begin
	EntryHigh = 0;
end;

vars: OpenCond(True);
OpenCond = True;
OpenCond = not (IsTXFSettlementDay and 1200 < Time and Time < 1400);
OpenCond = OpenCond and not (0845 <= Time and Time <= 0847);
OpenCond = OpenCond and not (1500 <= Time and Time <= 1502);

if MarketPosition = 0 then begin
	vars: BuyCond(True);
	BuyCond = OpenCond;
	BuyCond = BuyCond and High > BBandUp;
	BuyCond = BuyCond and Close > Open;
	BuyCond = BuyCond and Close > HH;
	if BuyCond then begin
		Buy ("LE") OrderSize contracts next bar market;
    end;

end else begin
	if IsTXFSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	vars: ProfitLevel(1);
	ProfitLevel = OpenPositionProfit / (ATR * BigPointValue * OrderSize);
	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - (2.1 - ProfitLevel * 0.2) * ATR * BigPointValue * OrderSize then begin
		Sell ("LX_Pullback") all contracts next bar market;
	end;

	if Close < EntryHigh - ATR * 7.5 then begin
		Sell ("LX_GiveUp") all contracts next bar market;
	end;
end;
