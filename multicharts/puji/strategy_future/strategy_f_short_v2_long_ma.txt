// TXF 43min
// Use bar 70
// Focus on balancing profits and losses.

inputs: Enabled(1), OrderSize(1);
inputs: F1(1), F2(1), C1(1), C2(1);

if Enabled = 0 then
	#Return;

vars: OpenPrice(Open);
if 0845 <= Time and Time <= 0850 then begin
	OpenPrice = Open;
end;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

if MarketPosition = 0 then begin
	vars: ShortCond(True);
	ShortCond = True;
	ShortCond = ShortCond and not (OverMonth and 1200 < Time and Time < 1400);
	ShortCond = ShortCond and not (0845 <= Time and Time <= 0847);
	ShortCond = ShortCond and Close < Open;
	ShortCond = ShortCond and Close < AverageFC(Close, 70);
	ShortCond = ShortCond and Close = Lowest(Close, 3);
	ShortCond = ShortCond and Ticks = Highest(Ticks, 15);
	if ShortCond then begin
		SellShort ( "SE" ) OrderSize contracts next bar market;
	end;

end else begin
	if IsSettlementDay and 1200 < Time and Time < 1400 then begin
		BuyToCover ("SX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - 35 * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback") all contracts next bar market;
	end;

	if BarsSinceEntry > 10 then begin
		BuyToCover ("SX_Timeout") all contracts next bar market;
	end;

	if OpenPositionProfit < 0 and BarsSinceEntry > 4 then begin
		BuyToCover ("SX_Giveup") all contracts next bar market;
	end;

	if OpenPositionProfit < -70 * BigPointValue * OrderSize then begin
		BuyToCover ("SX_StopLoss") all contracts next bar market;
	end;
end;
