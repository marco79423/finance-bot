// TXF 43min
// Use bar 70
// Focus on win rate and smooth out the curve

inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

if MarketPosition = 0 then begin
	vars: BuyCond(False);
	BuyCond = not (IsSettlementDay and 1200 < Time and Time < 1400);
	BuyCond = BuyCond and not (0845 <= Time and Time <= 0847);
	BuyCond = BuyCond and Close > Open;
	BuyCond = BuyCond and Close > AverageFC(Close, 65);
	BuyCond = BuyCond and Ticks = Highest(Ticks, 11);
	if BuyCond then begin
		Buy ("LE") OrderSize contracts next bar market;
	end;

end else begin
	if IsSettlementDay and 1200 < Time and Time < 1400 then begin
		Sell ("LX_Settle") all contracts next bar market;
	end;

	if OpenPositionProfit > 445 * BigPointValue * OrderSize then begin
		Sell("LX_Win") all contracts next bar market;
	end;

	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - 75 * BigPointValue * OrderSize then begin
		Sell ("LX_Pullback") all contracts next bar market;
	end;

	if OpenPositionProfit > 5 * BigPointValue * OrderSize and OpenPositionProfit < MaxOpenPositionProfit * 0.15 then begin
		Sell ("LX_Pullback2") all contracts next bar market;
	end;

	if BarsSinceEntry > 15 * 25 and OpenPositionProfit > 0 then begin
		Sell ("LX_Giveup") all contracts next bar market;
	end;

	if OpenPositionProfit < -860 * BigPointValue * OrderSize then begin
		Sell("LX_StopLoss") all contracts next bar market;
	end;
end;
