//TXF 4 min
inputs: Enabled(1), OrderSize(1);

if Enabled = 0 then
	#Return;

vars: MaxOpenPositionProfit(0);
if MarketPosition <> 0 then begin
	if MaxOpenPositionProfit < OpenPositionProfit then
		MaxOpenPositionProfit = OpenPositionProfit;
end else begin
	MaxOpenPositionProfit = 0;
end;

vars: LL(-999999);
If D > D[1] then begin
	LL = -999999;
end;

if Time = 0925 then begin
	LL = LowD(0);
end;

If MarketPosition = 0 then begin
	vars: ShortCond(True);
	ShortCond = True;
	ShortCond = ShortCond and Time >= 0925 and Time < 1300;
	ShortCond = ShortCond and OpenD(0) - CloseD(1) < -5;
	ShortCond = ShortCond and LowD(0) - CloseD(1) < -125;
	ShortCond = ShortCond and EntriesToday(D) <= 0;
	ShortCond = ShortCond and Close < LL;
	if ShortCond then begin
		SellShort ("SE") OrderSize contracts next bar market;
	end;
end else begin
	vars: Base(0);
	Base = AvgTrueRange(60);

	vars: BP(1);
	BP =  OpenPositionProfit / (Base * BigPointValue * OrderSize);
	if OpenPositionProfit > 0 and OpenPositionProfit < MaxOpenPositionProfit - (7.4 - BP * 0.8) * Base * BigPointValue * OrderSize then begin
		BuyToCover ("SX_Pullback") all contracts next bar market;
	end;

	if OpenPositionProfit < -90 * BigPointValue * OrderSize then begin
		BuyToCover ("SX_StopLoss") all contracts next bar market;
	end;

	If (IsSettlementDay and Time >= 1300) or (not IsSettlementDay and Time >= 1330) then begin
		BuyToCover ("SX_Timeout") next bar market;
	end;
end;
